options:
    server: "Minecraft X Valorant"



# Motd
on server list ping:
    set {mymotd} to "A &bM&2i&bn&2e&bc&2r&ba&2f&bt&r X &cV&ea&cl&eo&cr&ea&cn&et&9 distance PVP1.9&r server%nl%"
    chance of 20%:
        set the motd to "%{mymotd}%""Wherever I go, obliteration follows."""
    else:
        chance of 25%:
            set the motd to "%{mymotd}%""They should've dug a deeper grave."""
        else:
            chance of 33%:
                set the motd to "%{mymotd}%""Nobody escapes me. But they'll try."""
            else:
                chance of 50%:
                    set the motd to "%{mymotd}%""Stay alive! I want their life, not yours."""
                else:
                    set the motd to "%{mymotd}%""We're gonna win. No other option. Let's go."""


# Build mode
on join:
    set {minijeu::%player's uuid%.buildermode} to false

command /togglebuildmode:
    aliases: tggbuild, build, tggbm
    permission: op
    trigger:
        if {minijeu::%player's uuid%.buildermode} is false:
            set {minijeu::%player's uuid%.buildermode} to true
            send "Now, you can build !" to player
        else:
            set {minijeu::%player's uuid%.buildermode} to false
            send "Good ! You are secure from grief the world." to player
        
on block break:
    if {minijeu::%player's uuid%.buildermode} isn't set:
        set {minijeu::%player's uuid%.buildermode} to false
    if {minijeu::%player's uuid%.buildermode} is false:
        send "&cYou can't build and break now ! Use '/togglebuildmode' to modify the world !" to player
        cancel event

on block build:
    if {minijeu::%player's uuid%.buildermode} isn't set:
        set {minijeu::%player's uuid%.buildermode} to false
    if {minijeu::%player's uuid%.buildermode} is false:
        send "&cYou can't build and break now ! Use '/togglebuildmode' to modify the world !" to player
        cancel event

# Food system
every 10 seconds:
    loop all players:
        set loop-player's food level to 20

# World initialization
command /initgamerules:
    permission: op
    trigger:
        make player execute command "/gamerule announceAdvancements false"
        make player execute command "/gamerule commandBlockOutput false"
        make player execute command "/gamerule disableRaids true"
        make player execute command "/gamerule doMobLoot false"
        make player execute command "/gamerule doMobSpawning false"
        make player execute command "/gamerule doPatrolSpawning false"
        make player execute command "/gamerule doTraderSpawning false"
        make player execute command "/gamerule doVinesSpread false"
        make player execute command "/gamerule doWeatherCycle false"
        make player execute command "/gamerule drowningDamage false"
        make player execute command "/gamerule fallDamage false"
        make player execute command "/gamerule keepInventory true"
        make player execute command "/gamerule naturalRegeneration false"
        make player execute command "/gamerule sendCommandFeedback true"
        make player execute command "/gamerule showDeathMessages false"
        make player execute command "/gamerule spawnRadius 1"
        make player execute command "/gamerule spectatorsGenerateChunks false"



# Pvp system
command /pvp <boolean> [[in] <string>]:
    permission: op
    trigger:
        if arg-2 is set:
            if arg-1 is true:
                enable pvp in world arg-2
                send "&ePvP enabled in world ""%arg-2%""!" to player
            else:
                disable pvp in world arg-2
                send "&ePvP disabled in world ""%arg-2%""!" to player
        else:
            if arg-1 is true:
                enable pvp in all worlds
                send "&ePvP enabled !" to player
            else:
                disable pvp in all worlds
                send "&ePvP disabled !" to player


# Misc
on item damage:
    cancel event

on explode:
    event-entity is tnt
    cancel event
    create a safe explosion of force 5 at event-location

on pickup:
    cancel event


command /distance <int> <int> <int> [<int> <int> <int>]:
    trigger:
        if argument 4 is set:
            send "&l    %distance between the location (argument 1, argument 2, argument 3) and the location (argument 4, argument 5, argument 6)%" to player
        else:
            send "&l    %distance between the location (argument 1, argument 2, argument 3) and player%" to player

